apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "name.lmj0011.courierlocker"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 4
        versionName "1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        // ref: https://stackoverflow.com/a/48674264/2445763
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "DEBUG_MODE", "false")
        }
        debug {
            minifyEnabled false
            resValue("bool", "DEBUG_MODE", "true")
        }
    }
    flavorDimensions "default"
    productFlavors {
        dev { // active development/testing
            def devVersionCode = defaultConfig.versionCode + 2
            applicationIdSuffix ".dev"
            versionNameSuffix ".0.0-dev+${devVersionCode}"
            versionCode devVersionCode
        }
        beta { // public testing
            def betaVersionCode = defaultConfig.versionCode + 1
            applicationIdSuffix ".beta"
            versionNameSuffix ".0.0-beta+${betaVersionCode}"
            versionCode betaVersionCode
        }
        prod { // official release
            def prodVersionCode = defaultConfig.versionCode
            applicationIdSuffix ".prod"
            versionNameSuffix ".0.0+${prodVersionCode}"
            versionCode prodVersionCode
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version_ktx"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version_ktx"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Room dependencies
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // FusedLocationProviderClient dependencies
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // GSON
    implementation 'com.google.code.gson:gson:2.8.5'

    // Lifecycle-aware components
    // ref: https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // useful for testing with mock data
    implementation 'com.github.moove-it:fakeit:v0.7'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutine"

    // HTTP lib
    implementation "com.github.kittinunf.fuel:fuel:$version_kittinunf_fuel"
    implementation "com.github.kittinunf.fuel:fuel-json:$version_kittinunf_fuel"

    // androidx.preference
    implementation "androidx.preference:preference:1.1.0-alpha04"
}
